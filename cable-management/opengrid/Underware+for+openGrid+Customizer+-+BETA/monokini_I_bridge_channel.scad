/*Created by Hands on Katie and BlackjackDuck (Andy)
This code and all parts derived from it are Licensed Creative Commons 4.0 Attribution Non-Commercial Share-Alike (CC-BY-NC-SA)

Documentation available at https://handsonkatie.com/underware-2-0-the-made-to-measure-collection/

Change Log:
- 2024-12-06 
    - Initial release
- 2024-12-09
    - Fix to threading of snap connector by adding flare and new slop parameter
2025-03-20
    - Stronger profile options
2025-04-13
    - Initial implementation for openGrid


Credit to 
    First and foremost - Katie and her community at Hands on Katie on Youtube, Patreon, and Discord
    @David D on Printables for Multiconnect
    Jonathan at Keep Making for Multiboard
    @cosmicdust on MakerWorld and @freakadings_1408562 on Printables for the idea of diagonals (forward and turn)
    @siyrahfall+1155967 on Printables for the idea of top exit holes
    @Lyric on Printables for the flush connector idea
    @fawix on GitHub for her contributions on parameter descriptors
    PedroL on inital implementation of the monokini profile
    @BlackjackDuck on Printables for the original profiles and the idea of a channel


*/


include <BOSL2/std.scad>
include <BOSL2/rounding.scad>
include <BOSL2/threading.scad>



/*[Channel Size]*/
//Width (X axis) of channel in units. Default unit is 28mm
Channel_Width_in_Units = 1;  // Ensure this is an integer
//Height (Z axis) including connector (in mm)
Channel_Internal_Height_1 = 22; //[10:6:72]
//Incremental Height (Z axis) over bridge channel (in mm)
Channel_Internal_Height_2 = 22; //[10:6:72]
//Height (Z axis) of the below bridge channel (in mm)
Channel_Bridge_Height = 22; //[10:6:72] 
//Length (Y axis) of bridge channel in units. Default unit is 25mm
Channel_Length_Units = 1;
//Half Bridge instead of a full bridge. Length is honored.
Half_Bridge = false; // [true, false]


/*[Cord Cutouts]*/
Number_of_Cord_Cutouts = 0;
//Cutouts on left side, right side, or both (note that it can be flipped so left and right is moot)
Cord_Side_Cutouts = "Both Sides"; //[Left Side, Right Side, Both Sides, None]
//Width of each cord cutout (in mm)
Cord_Cutout_Width = 12;
// in mm
Distance_Between_Cutouts = 28;
//Distance (in mm) to offset cutouts along Y axis. Forward is positive, back is negative
Shift_Cutouts_Forward_or_Back = 0;

/*[Text Label]*/
//Labels (1) only work on MakerWorld, (2) must be exported as 3MF, and (3) must be imported into slicer as project (not geometry).
Add_Label = false;
//Text to appear on label
Text = "Hands on Katie";  // Text to be displayed
// Adjust the X axis position of the text
Text_x_coordinate = 0; 
//Depth of text (in mm). Increments of 0.2 to match layer heights. 
Text_Depth = 0.2; // [0:0.2:1] 
//Font must be installed on local machine if using local OpenSCAD
Font = "Raleway"; // [Asap, Bangers, Changa One, Chewy, Harmony OS Sans,Inter,Inter Tight,Lora,Merriweather Sans,Montserrat,Noto Emoji,Noto Sans,Noto Sans Adlam,Noto Sans Adlam Unjoined,Noto Sans Arabic,Noto Sans Arabic UI,Noto Sans Armenian,Noto Sans Balinese,Noto Sans Bamum,Noto Sans Bassa Vah,Noto Sans Bengali,Noto Sans Bengali UI,Noto Sans Canadian Aboriginal,Noto Sans Cham,Noto Sans Cherokee,Noto Sans Devanagari,Noto Sans Display,Noto Sans Ethiopic,Noto Sans Georgian,Noto Sans Gujarati,Noto Sans Gunjala Gondi,Noto Sans Gurmukhi,Noto Sans Gurmukhi UI,Noto Sans HK,Noto Sans Hanifi Rohingya,Noto Sans Hebrew,Noto Sans JP,Noto Sans Javanese,Noto Sans KR,Noto Sans Kannada,Noto Sans Kannada UI,Noto Sans Kawi,Noto Sans Kayah Li,Noto Sans Khmer,Noto Sans Khmer UI,Noto Sans Lao,Noto Sans Lao Looped,Noto Sans Lao UI,Noto Sans Lisu,Noto Sans Malayalam,Noto Sans Malayalam UI,Noto Sans Medefaidrin,Noto Sans Meetei Mayek,Noto Sans Mono,Noto Sans Myanmar,Noto Sans NKo Unjoined,Noto Sans Nag Mundari,Noto Sans New Tai Lue,Noto Sans Ol Chiki,Noto Sans Oriya,Noto Sans SC,Noto Sans Sinhala,Noto Sans Sinhala UI,Noto Sans Sora Sompeng,Noto Sans Sundanese,Noto Sans Symbols,Noto Sans Syriac,Noto Sans Syriac Eastern,Noto Sans TC,Noto Sans Tai Tham,Noto Sans Tamil,Noto Sans Tamil UI,Noto Sans Tangsa,Noto Sans Telugu,Noto Sans Telugu UI,Noto Sans Thaana,Noto Sans Thai,Noto Sans Thai UI,Noto Sans Vithkuqi,Nunito,Nunito Sans,Open Sans,Open Sans Condensed,Oswald,Playfair Display,Plus Jakarta Sans,Raleway,Roboto,Roboto Condensed,Roboto Flex,Roboto Mono,Roboto Serif,Roboto Slab,Rubik,Source Sans 3,Ubuntu Sans,Ubuntu Sans Mono,Work Sans]
//Styling of selecte font. Note that not all fonts support all styles. 
Font_Style = "Regular"; // [Regular,Bold,Medium,SemiBold,Light,ExtraBold,Black,ExtraLight,Thin,Bold Italic,Italic,Light Italic,Medium Italic]
Text_size = 10;    // Font size
//Color of label text (color names found at https://en.wikipedia.org/wiki/Web_colors)
Text_Color = "Pink";
// Emboss only
Text_Emboss = false; // [true, false]


/*[Advanced Options]*/
//Color of part (color names found at https://en.wikipedia.org/wiki/Web_colors)
Global_Color = "SlateBlue";
//Supression string Key: N - None, L - Left suppressed, R - Right suppressed, B - Both suppressed. With multiple channels, separate them with comma.
Suppress_Connectors = ""; // 
// Decorative option
Decorative = false; // [true, false] - If true, the profile will be decorative and not functional. This is useful for creating a decorative channel without the need for connectors or cutouts.
Decorative_Height = 1; // Height of the decorative profile in mm. This is only used if Decorative is true.
Decorative_Width = 17.6; // Width of the decorative profile in mm, subject to min and max size. This is only used if Decorative is true.
// Support bridging separation
Support_Bridging_Separation = 20; // Separation between the support bridges in mm. This is only used if Decorative is true.
// Grip flare in degrees
Grip_Flare = 0; // [0:0.1:15] 


/*[Hidden]*/
//Units of measurement (in mm) for hole and length spacing. Multiboard is 25mm. Untested
Grid_Size = 28;
channelWidthSeparation = 0.8; //distance between the two channels in the monokini profile
channelWidth = (Grid_Size-channelWidthSeparation*2) + (Channel_Width_in_Units-1) * Grid_Size;
lengthMM = Channel_Length_Units * Grid_Size * ( Half_Bridge ? 2 : 1);
baseHeight = 3.40;
topHeight = 18.60;
interlockOverlap = 3.40; //distance that the top and base overlap each other
interlockFromFloor = 3.40; //distance from the bottom of the base to the bottom of the top when interlocked
partSeparation = 10;
topChamfer = 4;
Nudge = 0.01; //nudge the profile to avoid z-fighting
snapWallThickness = 2;
gripSize = 15;
minWall = max(0.4, max(0, Decorative?((channelWidth - topChamfer*2 - 0.8) - Decorative_Width)/2:0));
surname_font = str(Font , ":style=", Font_Style);
Channel_Total_Height = max(Channel_Internal_Height_1, Channel_Internal_Height_2); //total height of the channel including the base and top
//The lateral distance of the rising portion of the channel
Rise_Distance = 28; //[12.5:12.5:100]
Over_Bridge_Height = max(Channel_Bridge_Height,Channel_Internal_Height_1)+ Channel_Internal_Height_2;

Suppress_List = str_split(upcase(Suppress_Connectors), ",");
//Convert the string to a list of strings. This is used to determine if the connector should be suppressed or not.

///*[Visual Options]*/
Debug_Show_Grid = false;
//View the parts as they attach. Note that you must disable this before exporting for printing. 
Show_Attached = false;


//module mw_plate_1 {
back_half(s=max(100,lengthMM,2*Over_Bridge_Height+Grid_Size*2),y =(Half_Bridge ? 0 : -lengthMM*2))
color(Global_Color) difference() { 
union() {
back(lengthMM/2+Grid_Size/2)
monokiniHeightChangeChannel(lengthMM = Grid_Size, widthMM = channelWidth, heightMM1 = Channel_Internal_Height_1, heightMM2 = Over_Bridge_Height, anchor = CENTER, orient = TOP, spin = 0, suppress = Suppress_List[0]); //str_pad(Suppress_List[0], Channel_Length_Units, "N")
if (!Half_Bridge)
    yflip()
    back(lengthMM/2+Grid_Size/2)
    monokiniHeightChangeChannel(lengthMM = Grid_Size, widthMM = channelWidth, heightMM1 = Channel_Internal_Height_1, heightMM2 = Over_Bridge_Height, anchor = CENTER, orient = TOP, spin = 0, suppress = Suppress_List[1]); //str_pad(Suppress_List[0], Channel_Length_Units, "N")
diff() {
monokiniChannel(lengthMM = lengthMM, widthMM = channelWidth, heightMM = Over_Bridge_Height, anchor = CENTER, orient = TOP, spin = 0, suppress= str_pad("", 2*Channel_Length_Units, "B"))


  if (Cord_Side_Cutouts != "None" && Number_of_Cord_Cutouts > 0) {
    /*
                translate(v = [internalWidth/2+cordCutoutLateralOffset,internalDepth/2+cordCutoutDepthOffset,-1]) {
                    union(){
                        cylinder(h = baseThickness + frontLowerCapture + 2, r = cordCutoutDiameter/2);
                        translate(v = [-cordCutoutDiameter/2,0,0]) cube([cordCutoutDiameter,internalWidth/2+wallThickness+1,baseThickness + frontLowerCapture + 2]);
                    }
                }
                */
                tag("remove") color_this(Global_Color) attach(CENTER) up(snapWallThickness) fwd(Shift_Cutouts_Forward_or_Back) {
                    ycopies(n=Number_of_Cord_Cutouts, spacing=Distance_Between_Cutouts) {
                        left(Cord_Side_Cutouts == "Right Side" ? channelWidth/2 : 0)
                        left(Cord_Side_Cutouts == "Left Side" ? -channelWidth/2 : 0)
                            // cuboid([Cord_Side_Cutouts == "Both Sides" ? channelWidth + 5 : channelWidth/2, Cord_Cutout_Width, Channel_Total_Height-snapWallThickness], chamfer = 2, edges=[BOT+FWD, BOT+BACK], orient=TOP, anchor=BOTTOM);
                            up(snapWallThickness) color_this(Global_Color) cylinder(h = Cord_Side_Cutouts == "Both Sides" ? channelWidth + 5 : channelWidth/2, r = Cord_Cutout_Width/2, orient=LEFT, anchor=LEFT, $fn=50)
                            attach(RIGHT,BOT, overlap=Cord_Cutout_Width/2) color_this(Global_Color) cube([Cord_Side_Cutouts == "Both Sides" ? channelWidth + 5 : channelWidth/2, Cord_Cutout_Width, Channel_Total_Height-snapWallThickness-Cord_Cutout_Width/2], spin=90);
                    }
                }

            }
            
if(Add_Label) tag("remove") 
    up(Text_Depth > 0.05 ? Text_Depth/2 : 0.03) right(Text_x_coordinate) text3d(Text, size = Text_size, h=Text_Depth > 0.05 ? Nudge+Text_Depth : 0.06, font = surname_font, atype="ycenter", anchor=CENTER, spin=-90, orient=BOT);
//}

if(Add_Label && !Text_Emboss) tag("keep") recolor(Text_Color)
    up(Text_Depth/2-Nudge) right(Text_x_coordinate) text3d(Text, size = Text_size, h=Text_Depth > 0.05 ? Text_Depth : 0.05, font = surname_font, atype="ycenter", anchor=CENTER, spin=-90, orient=BOT);

}
}
up(Over_Bridge_Height-Channel_Bridge_Height)
hull()
  monokiniChannel(lengthMM = 2*Grid_Size+channelWidth, widthMM = lengthMM, heightMM = Channel_Bridge_Height, anchor = CENTER, orient = TOP, spin = 90);
}

/*

***BEGIN MODULES***

*/

module monokiniGrip(widthMM = 26.4, heightMM = 22, flareAngle = 0) {

        
        snapCaptureStrength = 0.7;
        baseChamfer = 0.5;
        topLittleChamfer = 0.4;
        spacingFromChannel = 6.5;
        
        chamferPosY = spacingFromChannel - (baseHeight - snapWallThickness);
        chamferPosY2 = gripSize + spacingFromChannel + (baseHeight - snapWallThickness);

        

 monokiniProfileGrip =
        [
            [snapCaptureStrength,0],
            [snapCaptureStrength, baseHeight-snapWallThickness],
            [0, baseHeight-snapWallThickness/2],
            [0, baseHeight-topLittleChamfer],
            [snapCaptureStrength, baseHeight],
            [snapWallThickness, baseHeight],
            [snapWallThickness, 0]
        ];


    gripPath = [[-1*widthMM/2,spacingFromChannel],[-1*widthMM/2,gripSize+spacingFromChannel]];
    up(heightMM-baseHeight) 
    //yrot(-Grip_Flare,cp=gripPath[0]) //rotate the grip path to match the flare
    skew(axz=-flareAngle)
    difference() {
        path_extrude2d( gripPath ) 
            polygon(monokiniProfileGrip); 
            //path_extrude(path, shape, anchor=BOTTOM, orient=TOP, spin=0, size=[2,2], $fn=50)
            up(baseHeight)
            left((widthMM/2)-snapWallThickness)
            back(chamferPosY) 
            zrot(-90)
            yrot(180) 
            xrot(90)
            linear_extrude(snapWallThickness) 
            right_triangle([baseHeight,baseHeight]);
            up(baseHeight)
            left(widthMM/2)
            back(chamferPosY2) 
            zrot(90)
            yrot(180) 
            xrot(90)
            linear_extrude(snapWallThickness) 
            right_triangle([baseHeight,baseHeight]);
    }
}


module monokiniHeightChangeChannel(lengthMM = 28, widthMM = 26.4, heightMM1 = 22, heightMM2 = 22, anchor, spin, orient, suppress = ""){
    
    off1 = max(heightMM1, heightMM2) - heightMM1; //offset to make sure the first profile is always the highest
    off2 = max(heightMM1, heightMM2) - heightMM2; //offset to make sure the second profile is always the highest

    heightMM = max(heightMM1, heightMM2); //height of the channel
    AmonokiniProfile = [
            [-1*widthMM/2, off1+heightMM1-baseHeight],
            [-1*widthMM/2, off1+topChamfer],
            [-1*widthMM/2+topChamfer, Decorative?(off1-Decorative_Height):off1],
            [-1*widthMM/2+topChamfer+minWall, Decorative?(off1-Decorative_Height):off1],
            [-1*widthMM/2+topChamfer+minWall+Nudge, off1],
            [widthMM/2-topChamfer-minWall-Nudge, off1],
            [widthMM/2-topChamfer-minWall, Decorative?(off1-Decorative_Height):off1],
            [widthMM/2-topChamfer, Decorative?(off1-Decorative_Height):off1],
            [widthMM/2, off1+topChamfer],
            [widthMM/2, off1+heightMM1-baseHeight]
        ];
    
    Aroff_Profile = offset(deduplicate(AmonokiniProfile), delta=-snapWallThickness, check_valid=true); //create the monokini profile
    Apoint1 = select(AmonokiniProfile, 0); //get the first point of the profile
    Apoint2 = select(Aroff_Profile, 0); //get the first point of the offset profile
    Apoint3 = select(Aroff_Profile, -1); //get the last point of the offset profile
    Aroff_Profile2 = list_set(Aroff_Profile, 0, [Apoint2[0], Apoint1[1]]); //fix the first point to be the same as the original profile
    Aroff_Profile3 = list_set(Aroff_Profile2, -1, [Apoint3[0], Apoint1[1]]); //fix the first point to be the same as the original profile

    Aoff_Profile2 = concat(AmonokiniProfile,reverse(Aroff_Profile3)); //reverse the order of the points to match the original profile

    BmonokiniProfile = [
            [-1*widthMM/2, off2+heightMM2-baseHeight],
            [-1*widthMM/2, off2+topChamfer],
            [-1*widthMM/2+topChamfer, Decorative?(off2-Decorative_Height):off2],
            [-1*widthMM/2+topChamfer+minWall, Decorative?(off2-Decorative_Height):off2],
            [-1*widthMM/2+topChamfer+minWall+Nudge, off2],
            [widthMM/2-topChamfer-minWall-Nudge, off2],
            [widthMM/2-topChamfer-minWall, Decorative?(off2-Decorative_Height):off2],
            [widthMM/2-topChamfer, Decorative?(off2-Decorative_Height):off2],
            [widthMM/2, off2+topChamfer],
            [widthMM/2, off2+heightMM2-baseHeight]
        ];
    
    Broff_Profile = offset(deduplicate(BmonokiniProfile), delta=-snapWallThickness, check_valid=true); //create the monokini profile
    Bpoint1 = select(BmonokiniProfile, 0); //get the first point of the profile
    Bpoint2 = select(Broff_Profile, 0); //get the first point of the offset profile
    Bpoint3 = select(Broff_Profile, -1); //get the last point of the offset profile
    Broff_Profile2 = list_set(Broff_Profile, 0, [Bpoint2[0], Bpoint1[1]]); //fix the first point to be the same as the original profile
    Broff_Profile3 = list_set(Broff_Profile2, -1, [Bpoint3[0], Bpoint1[1]]); //fix the first point to be the same as the original profile

    Boff_Profile2 = concat(BmonokiniProfile,reverse(Broff_Profile3)); //reverse the order of the points to match the original profile

pathChannel = [[0,0],[0,lengthMM-Nudge]];

    attachable(anchor, spin, orient, size=[widthMM, lengthMM, baseHeight + (heightMM2-12)])  {
        fwd(lengthMM/2) 
        difference() {
    union() {
        back(lengthMM-Nudge/2)
        xrot(90)
        skin(
            [
                Aoff_Profile2,
                Aoff_Profile2,
                Boff_Profile2,
                Boff_Profile2
            ],
            z=[0,lengthMM/2-Rise_Distance/2,lengthMM/2+Rise_Distance/2,lengthMM],
            slices = 0
            );
    
    
    if (lengthMM > gripSize)
        path_copies(pathChannel, spacing=Grid_Size) {
            right(Grid_Size/2)
            zrot(90)
            if ((($idx+1)*Grid_Size)<=(lengthMM/2-Rise_Distance/2)) {
            up(off2-topChamfer) {
            echo("AAA Index: ", $idx, " Suppress: ", suppress[$idx], " Grip_Flare: ", Grip_Flare);
                if (suppress[$idx] != "B" && suppress[$idx] != "L")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        monokiniGripFlared(widthMM = widthMM, heightMM = heightMM2, flareAngle = Grip_Flare);
                    else
                        down(22-heightMM) down(off2-topChamfer) monokiniGrip(widthMM = widthMM);
                if (suppress[$idx] != "B" && suppress[$idx] != "R")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        xflip() monokiniGripFlared(widthMM = widthMM, heightMM = heightMM2, flareAngle = Grip_Flare);
                    else
                        down(22-heightMM) down(off2-topChamfer) xflip() monokiniGrip(widthMM = widthMM);
            }
            } else if (($idx*Grid_Size)>=(lengthMM/2+Rise_Distance/2)) {
                up(off1-topChamfer) {
            echo("BBB Index: ", $idx, " Suppress: ", suppress[$idx], " Grip_Flare: ", Grip_Flare);
                if (suppress[$idx] != "B" && suppress[$idx] != "L")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        monokiniGripFlared(widthMM = widthMM, heightMM = heightMM1, flareAngle = Grip_Flare);
                    else
                        down(22-heightMM) down(off1-topChamfer) monokiniGrip(widthMM = widthMM);
                if (suppress[$idx] != "B" && suppress[$idx] != "R")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        xflip() monokiniGripFlared(widthMM = widthMM, heightMM = heightMM1, flareAngle = Grip_Flare);
                    else
                        down(22-heightMM) down(off1-topChamfer) xflip() monokiniGrip(widthMM = widthMM);
            }
            } else {
                //down(22-heightMM) {
            echo("CCC Index: ", $idx, " Suppress: ", suppress[$idx], " Grip_Flare: ", Grip_Flare);
                
                if (suppress[$idx] != "B" && suppress[$idx] != "L")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        //up(heightMM1-heightMM2+22-topChamfer) 
                        up(heightMM2-heightMM1-topChamfer)
                        monokiniGripFlared(widthMM = widthMM, heightMM = heightMM1, flareAngle = Grip_Flare);
                    else
                        down(22-heightMM) monokiniGrip(widthMM = widthMM);
                if (suppress[$idx] != "B" && suppress[$idx] != "R")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        xflip() 
                        up(heightMM2-heightMM1-topChamfer)
                        monokiniGripFlared(widthMM = widthMM, heightMM = heightMM1, flareAngle = Grip_Flare);
                    else
                        down(22-heightMM) xflip() monokiniGrip(widthMM = widthMM);
                
                /* if (suppress[$idx] != "B" && suppress[$idx] != "L")
                    
                        monokiniGrip(widthMM = widthMM);
                if (suppress[$idx] != "B" && suppress[$idx] != "R")
                    
                        xflip() monokiniGrip(widthMM = widthMM); */
            //}
            }
        }
    }
    if (lengthMM > gripSize)
        path_copies(pathChannel, spacing=Grid_Size) {
            left(Grid_Size/2)
            zrot(90) 
            //down(22-heightMM) {
                if (!((($idx+1)*Grid_Size)>=(lengthMM/2-Rise_Distance/2) && (($idx*Grid_Size)<=(lengthMM/2+Rise_Distance/2)) )) {
            // echo("Index: ", $idx, " Suppress: ", suppress[$idx]);
                if (suppress[$idx] != "B" && suppress[$idx] != "L")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        monokiniGripFlaredCut(widthMM = widthMM, heightMM = heightMM, flareAngle = Grip_Flare);
                    // else
                        // monokiniGrip(widthMM = widthMM);
                if (suppress[$idx] != "B" && suppress[$idx] != "R")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        xflip() monokiniGripFlaredCut(widthMM = widthMM, heightMM = heightMM, flareAngle = Grip_Flare);
                    // else
                        // xflip() monokiniGrip(widthMM = widthMM);
                } else {
                    if (suppress[$idx] != "B" && suppress[$idx] != "L")
                        if (Grip_Flare > 0 && suppress[$idx] != "S")
                            //down(22-heightMM) 
                            up(heightMM2-heightMM1)
                            monokiniGripFlaredCut(widthMM = widthMM, heightMM = heightMM1, flareAngle = Grip_Flare);
                    
                    if (suppress[$idx] != "B" && suppress[$idx] != "R")
                        if (Grip_Flare > 0 && suppress[$idx] != "S")
                            xflip() 
                            //down(22-heightMM) 
                            up(heightMM2-heightMM1)
                            monokiniGripFlaredCut(widthMM = widthMM, heightMM = heightMM1, flareAngle = Grip_Flare);

                }
            //}
        }
    }
    children();
    }
    

}



module monokiniChannel(lengthMM = 28, widthMM = 26.4, heightMM = 22, anchor, spin, orient, suppress = "") {
    
    // echo("Suppress: ", suppress);
    // zrot(180) xrot(90) path_extrude( monokiniProfile()) square([2,channelWidth]); //path_extrude(path, shape, anchor=BOTTOM, orient=TOP, spin=0, size=[2,2], $fn=50)
    monokiniProfile = [
            [-1*widthMM/2, heightMM-baseHeight],
            [-1*widthMM/2, topChamfer],
            [-1*widthMM/2+topChamfer, Decorative?-Decorative_Height:0],
            [-1*widthMM/2+topChamfer+minWall, Decorative?-Decorative_Height:0],
            [-1*widthMM/2+topChamfer+minWall+Nudge, 0],
            [widthMM/2-topChamfer-minWall-Nudge, 0],
            [widthMM/2-topChamfer-minWall, Decorative?-Decorative_Height:0],
            [widthMM/2-topChamfer, Decorative?-Decorative_Height:0],
            [widthMM/2, topChamfer],
            [widthMM/2, heightMM-baseHeight]
        ];

    
    
//stroke(monokiniProfile);



    pathChannel = [[0,0],[0,lengthMM-Nudge]];
 
    roff_Profile = offset(deduplicate(monokiniProfile), delta=-snapWallThickness, check_valid=true); //create the monokini profile
    point1 = select(monokiniProfile, 0); //get the first point of the profile
    point2 = select(roff_Profile, 0); //get the first point of the offset profile
    point3 = select(roff_Profile, -1); //get the last point of the offset profile
    roff_Profile2 = list_set(roff_Profile, 0, [point2[0], point1[1]]); //fix the first point to be the same as the original profile
    roff_Profile3 = list_set(roff_Profile2, -1, [point3[0], point1[1]]); //fix the first point to be the same as the original profile

    off_Profile2 = concat(monokiniProfile,reverse(roff_Profile3)); //reverse the order of the points to match the original profile

    supportProfile = [
            [-Decorative_Height, -Decorative_Height],
            [-0.1,-0.2],
            [0,0],
            [0.1,-0.2],
            [Decorative_Height, -Decorative_Height]
        ];

 
    attachable(anchor, spin, orient, size=[widthMM, lengthMM, topHeight + (heightMM-12)]){
    fwd(lengthMM/2) 
    difference() {
    union() {
    back(lengthMM-Nudge/2) xrot(90) linear_extrude(height=lengthMM+Nudge) 
    polygon(off_Profile2);
    if (Decorative) {
        back(lengthMM-Nudge) xrot(90) linear_extrude(height=lengthMM+Nudge) 
        xcopies(l=widthMM-topChamfer*2-Decorative_Height*2-minWall*2, spacing=Support_Bridging_Separation)
        polygon(supportProfile);
    }
    
     if (lengthMM > gripSize)
        path_copies(pathChannel, spacing=Grid_Size) {
            right(Grid_Size/2)
            zrot(90) 
            down(22-heightMM) {
            // echo("Index: ", $idx, " Suppress: ", suppress[$idx]);
                if (suppress[$idx] != "B" && suppress[$idx] != "L")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        monokiniGripFlared(widthMM = widthMM, heightMM = heightMM, flareAngle = Grip_Flare);
                    else
                        monokiniGrip(widthMM = widthMM);
                if (suppress[$idx] != "B" && suppress[$idx] != "R")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        xflip() monokiniGripFlared(widthMM = widthMM, heightMM = heightMM, flareAngle = Grip_Flare);
                    else
                        xflip() monokiniGrip(widthMM = widthMM);
            }
        }
    }
    if (lengthMM > gripSize)
        path_copies(pathChannel, spacing=Grid_Size) {
            left(Grid_Size/2)
            zrot(90) 
            down(22-heightMM) {
            // echo("Index: ", $idx, " Suppress: ", suppress[$idx]);
                if (suppress[$idx] != "B" && suppress[$idx] != "L")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        monokiniGripFlaredCut(widthMM = widthMM, heightMM = heightMM, flareAngle = Grip_Flare);
                    // else
                        // monokiniGrip(widthMM = widthMM);
                if (suppress[$idx] != "B" && suppress[$idx] != "R")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        xflip() monokiniGripFlaredCut(widthMM = widthMM, heightMM = heightMM, flareAngle = Grip_Flare);
                    // else
                        // xflip() monokiniGrip(widthMM = widthMM);
            }
        }
    } children();
    } 

    
 }


module monokiniGripFlared(widthMM = 26.4, heightMM = 22, flareAngle = 0) {

        
        snapCaptureStrength = 0.7;
        baseChamfer = 0.5;
        topLittleChamfer = 0.4;
        spacingFromChannel = 6.5;
        
        chamferPosY = spacingFromChannel - (baseHeight - snapWallThickness);
        chamferPosY2 = gripSize + spacingFromChannel + (baseHeight - snapWallThickness);

        

 monokiniProfileGrip =
        [
            [snapCaptureStrength,topChamfer],
            [snapCaptureStrength, heightMM-snapWallThickness],
            [0, heightMM-snapWallThickness/2],
            [0, heightMM-topLittleChamfer],
            [snapCaptureStrength, heightMM],
            [snapWallThickness, heightMM],
            [snapWallThickness, topChamfer]
        ];


    gripPath = [[-1*widthMM/2,spacingFromChannel],[-1*widthMM/2,gripSize+spacingFromChannel]];
    up(topChamfer) 
    //yrot(-Grip_Flare,cp=gripPath[0]) //rotate the grip path to match the flare
    skew(axz=-flareAngle)
    difference() {
        path_extrude2d( gripPath ) 
            polygon(monokiniProfileGrip); 
            //path_extrude(path, shape, anchor=BOTTOM, orient=TOP, spin=0, size=[2,2], $fn=50)
            up(heightMM-baseHeight) {
            up(baseHeight)
            left((widthMM/2)-snapWallThickness)
            back(chamferPosY) 
            zrot(-90)
            yrot(180) 
            xrot(90)
            linear_extrude(snapWallThickness) 
            right_triangle([baseHeight,baseHeight]);
            up(baseHeight)
            left(widthMM/2)
            back(chamferPosY2) 
            zrot(90)
            yrot(180) 
            xrot(90)
            linear_extrude(snapWallThickness) 
            right_triangle([baseHeight,baseHeight]);
            }
    }
}

module monokiniGripFlaredCut(widthMM = 26.4, heightMM = 22, flareAngle = 0) {

        
        snapCaptureStrength = 0.7;
        baseChamfer = 0.5;
        topLittleChamfer = 0.4;
        spacingFromChannel = 6.5;
        
        chamferPosY = spacingFromChannel - (baseHeight - snapWallThickness);
        chamferPosY2 = gripSize + spacingFromChannel + (baseHeight - snapWallThickness);

        up(topChamfer)
        left(widthMM/2-Nudge) {
            fwd(spacingFromChannel-(topLittleChamfer/2)) cube([Nudge+snapWallThickness*2, topLittleChamfer, heightMM-baseHeight], anchor=BOTTOM);
            fwd(gripSize+spacingFromChannel+(topLittleChamfer/2)) cube([Nudge+snapWallThickness*2, topLittleChamfer, heightMM-baseHeight], anchor=BOTTOM);
        }
        
/*
 monokiniProfileGrip =
        [
            [snapCaptureStrength,topChamfer],
            [snapCaptureStrength, heightMM-snapWallThickness],
            [0, heightMM-snapWallThickness/2],
            [0, heightMM-topLittleChamfer],
            [snapCaptureStrength, heightMM],
            [snapWallThickness, heightMM],
            [snapWallThickness, topChamfer]
        ];


    gripPath = [[-1*widthMM/2,spacingFromChannel],[-1*widthMM/2,gripSize+spacingFromChannel]];
    //up(heightMM-baseHeight) 
    //yrot(-Grip_Flare,cp=gripPath[0]) //rotate the grip path to match the flare
    skew(axz=-flareAngle)
    difference() {
        path_extrude2d( gripPath ) 
            polygon(monokiniProfileGrip); 
            //path_extrude(path, shape, anchor=BOTTOM, orient=TOP, spin=0, size=[2,2], $fn=50)
            up(heightMM-baseHeight) {
            up(baseHeight)
            left((widthMM/2)-snapWallThickness)
            back(chamferPosY) 
            zrot(-90)
            yrot(180) 
            xrot(90)
            linear_extrude(snapWallThickness) 
            right_triangle([baseHeight,baseHeight]);
            up(baseHeight)
            left(widthMM/2)
            back(chamferPosY2) 
            zrot(90)
            yrot(180) 
            xrot(90)
            linear_extrude(snapWallThickness) 
            right_triangle([baseHeight,baseHeight]);
            }
    }
    */
}


//calculate the max x and y points. Useful in calculating size of an object when the path are all positive variables originating from [0,0]
function maxX(path) = max([for (p = path) p[0]]) + abs(min([for (p = path) p[0]]));
function maxY(path) = max([for (p = path) p[1]]) + abs(min([for (p = path) p[1]]));
