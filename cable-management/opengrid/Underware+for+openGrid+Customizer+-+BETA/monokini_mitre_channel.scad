/*Created by Hands on Katie and BlackjackDuck (Andy)
This code and all parts derived from it are Licensed Creative Commons 4.0 Attribution Non-Commercial Share-Alike (CC-BY-NC-SA)

Documentation available at https://handsonkatie.com/underware-2-0-the-made-to-measure-collection/

Change Log:
- 2024-12-06 
    - Initial release
- 2024-12-09
    - Fix to threading of snap connector by adding flare and new slop parameter
2025-03-20
    - Stronger profile options
2025-04-13
    - Initial implementation for openGrid


Credit to 
    First and foremost - Katie and her community at Hands on Katie on Youtube, Patreon, and Discord
    @David D on Printables for Multiconnect
    Jonathan at Keep Making for Multiboard
    @cosmicdust on MakerWorld and @freakadings_1408562 on Printables for the idea of diagonals (forward and turn)
    @siyrahfall+1155967 on Printables for the idea of top exit holes
    @Lyric on Printables for the flush connector idea
    @fawix on GitHub for her contributions on parameter descriptors
    PedroL on inital implementation of the monokini profile
    @BlackjackDuck on Printables for the original profiles and the idea of a channel


*/


include <BOSL2/std.scad>
include <BOSL2/rounding.scad>
include <BOSL2/threading.scad>



/*[Channel Size]*/
//Width (X axis) of channel in units. Default unit is 28mm
Channel_Width_in_Units = 1;  // Ensure this is an integer
//Height (Z axis) including connector (in mm)
Channel_Total_Height = 22; //[10:6:72]
//Number of grids extending along the Y axis from the corner grid in units (default unit is 28mm)
L_Channel_Length_in_Units_Y_Axis = 1;
//Number of grids extending along the X axis from the corner grid in units (default unit is 28mm)
L_Channel_Length_in_Units_X_Axis = 1;
//If true, the channel will be mirrored along the Y axis. This is useful for creating a channel that is symmetrical along the Y axis.
Mirror_Channel = false; //[true, false]
//Add units to channel radius
Added_Radius = 0; 

/*[Cord Cutouts]*/
Number_of_Cord_Cutouts = 0;
//Cutouts on left side, right side, or both (note that it can be flipped so left and right is moot)
Cord_Side_Cutouts = "Both Sides"; //[Left Side, Right Side, Both Sides, None, Left Side Top, Right Side Top]
//Width of each cord cutout (in mm)
Cord_Cutout_Width = 12;
// in mm
Distance_Between_Cutouts = 28;
//Distance (in mm) to offset cutouts along Y axis. Forward is positive, back is negative
Shift_Cutouts_Forward_or_Back = 0;

/*[Text Label]*/
//Labels (1) only work on MakerWorld, (2) must be exported as 3MF, and (3) must be imported into slicer as project (not geometry).
Add_Label = false;
//Text to appear on label
Text = "Hands on Katie";  // Text to be displayed
// Adjust the X axis position of the text
Text_x_coordinate = 0; 
//Depth of text (in mm). Increments of 0.2 to match layer heights. 
Text_Depth = 0.2; // [0:0.2:1] 
//Font must be installed on local machine if using local OpenSCAD
Font = "Raleway"; // [Asap, Bangers, Changa One, Chewy, Harmony OS Sans,Inter,Inter Tight,Lora,Merriweather Sans,Montserrat,Noto Emoji,Noto Sans,Noto Sans Adlam,Noto Sans Adlam Unjoined,Noto Sans Arabic,Noto Sans Arabic UI,Noto Sans Armenian,Noto Sans Balinese,Noto Sans Bamum,Noto Sans Bassa Vah,Noto Sans Bengali,Noto Sans Bengali UI,Noto Sans Canadian Aboriginal,Noto Sans Cham,Noto Sans Cherokee,Noto Sans Devanagari,Noto Sans Display,Noto Sans Ethiopic,Noto Sans Georgian,Noto Sans Gujarati,Noto Sans Gunjala Gondi,Noto Sans Gurmukhi,Noto Sans Gurmukhi UI,Noto Sans HK,Noto Sans Hanifi Rohingya,Noto Sans Hebrew,Noto Sans JP,Noto Sans Javanese,Noto Sans KR,Noto Sans Kannada,Noto Sans Kannada UI,Noto Sans Kawi,Noto Sans Kayah Li,Noto Sans Khmer,Noto Sans Khmer UI,Noto Sans Lao,Noto Sans Lao Looped,Noto Sans Lao UI,Noto Sans Lisu,Noto Sans Malayalam,Noto Sans Malayalam UI,Noto Sans Medefaidrin,Noto Sans Meetei Mayek,Noto Sans Mono,Noto Sans Myanmar,Noto Sans NKo Unjoined,Noto Sans Nag Mundari,Noto Sans New Tai Lue,Noto Sans Ol Chiki,Noto Sans Oriya,Noto Sans SC,Noto Sans Sinhala,Noto Sans Sinhala UI,Noto Sans Sora Sompeng,Noto Sans Sundanese,Noto Sans Symbols,Noto Sans Syriac,Noto Sans Syriac Eastern,Noto Sans TC,Noto Sans Tai Tham,Noto Sans Tamil,Noto Sans Tamil UI,Noto Sans Tangsa,Noto Sans Telugu,Noto Sans Telugu UI,Noto Sans Thaana,Noto Sans Thai,Noto Sans Thai UI,Noto Sans Vithkuqi,Nunito,Nunito Sans,Open Sans,Open Sans Condensed,Oswald,Playfair Display,Plus Jakarta Sans,Raleway,Roboto,Roboto Condensed,Roboto Flex,Roboto Mono,Roboto Serif,Roboto Slab,Rubik,Source Sans 3,Ubuntu Sans,Ubuntu Sans Mono,Work Sans]
//Styling of selecte font. Note that not all fonts support all styles. 
Font_Style = "Regular"; // [Regular,Bold,Medium,SemiBold,Light,ExtraBold,Black,ExtraLight,Thin,Bold Italic,Italic,Light Italic,Medium Italic]
Text_size = 10;    // Font size
//Color of label text (color names found at https://en.wikipedia.org/wiki/Web_colors)
Text_Color = "Pink";
// Emboss only
Text_Emboss = false; // [true, false]


/*[Advanced Options]*/
//Color of part (color names found at https://en.wikipedia.org/wiki/Web_colors)
Global_Color = "SlateBlue";
//Supression string Key: N - None, L - Left suppressed, R - Right suppressed, B - Both suppressed. With multiple channels, separate them with comma.
Suppress_Connectors = ""; // 
// Decorative option
Decorative = false; // [true, false] - If true, the profile will be decorative and not functional. This is useful for creating a decorative channel without the need for connectors or cutouts.
Decorative_Height = 1; // Height of the decorative profile in mm. This is only used if Decorative is true.
Decorative_Width = 17.6; // Width of the decorative profile in mm, subject to min and max size. This is only used if Decorative is true.
// Support bridging separation
Support_Bridging_Separation = 20; // Separation between the support bridges in mm. This is only used if Decorative is true.
// Grip flare in degrees
Grip_Flare = 0; // [0:0.1:15]
// Channel Separators Key: N - None, S - Separator, separate channels by commas, blank is all None
Channel_Separators = ""; //


/*[Hidden]*/
//Units of measurement (in mm) for hole and length spacing. Multiboard is 25mm. Untested
Grid_Size = 28;
channelWidthSeparation = 0.8; //distance between the two channels in the monokini profile
channelWidth = (Grid_Size-channelWidthSeparation*2) + (Channel_Width_in_Units-1) * Grid_Size;
curveWidth = Channel_Width_in_Units * Grid_Size;
lengthMM = L_Channel_Length_in_Units_Y_Axis * Grid_Size;
lengthMM2 = L_Channel_Length_in_Units_X_Axis * Grid_Size;
baseHeight = 3.40;
topHeight = 18.60;
interlockOverlap = 3.40; //distance that the top and base overlap each other
interlockFromFloor = 3.40; //distance from the bottom of the base to the bottom of the top when interlocked
partSeparation = 10;
topChamfer = 4;
Nudge = 0.01; //nudge the profile to avoid z-fighting
snapWallThickness = 2;
gripSize = 15;
minWall = max(0.4, max(0, Decorative?((channelWidth - topChamfer*2 - 0.8) - Decorative_Width)/2:0));
surname_font = str(Font , ":style=", Font_Style);

Suppress_List = str_split(upcase(Suppress_Connectors), ",");
//Convert the string to a list of strings. This is used to determine if the connector should be suppressed or not.
Separator_List = str_split(upcase(Channel_Separators), ",");

///*[Visual Options]*/
Debug_Show_Grid = false;
//View the parts as they attach. Note that you must disable this before exporting for printing. 
Show_Attached = false;


//module mw_plate_1 {
if (Mirror_Channel) {
    xflip() actually_channel();
    } else {
        actually_channel();
    }

module actually_channel() {
union() {
left(Added_Radius*Grid_Size)
fwd(lengthMM/2+curveWidth/2)
diff() {
color_this(Global_Color) 
  monokiniChannel(lengthMM = lengthMM, widthMM = channelWidth, heightMM = Channel_Total_Height, anchor = CENTER, orient = TOP, spin = 0, suppress = Suppress_List[0], separator = Separator_List[0]);

  if (Cord_Side_Cutouts != "None" && Number_of_Cord_Cutouts > 0) {
    /*
                translate(v = [internalWidth/2+cordCutoutLateralOffset,internalDepth/2+cordCutoutDepthOffset,-1]) {
                    union(){
                        cylinder(h = baseThickness + frontLowerCapture + 2, r = cordCutoutDiameter/2);
                        translate(v = [-cordCutoutDiameter/2,0,0]) cube([cordCutoutDiameter,internalWidth/2+wallThickness+1,baseThickness + frontLowerCapture + 2]);
                    }
                }
                */
                tag("remove") color_this(Global_Color) 
                //attach(CENTER) 
                up(snapWallThickness) fwd(Shift_Cutouts_Forward_or_Back) {
                    ycopies(n=Number_of_Cord_Cutouts, spacing=Distance_Between_Cutouts) {
                        if (Cord_Side_Cutouts == "Left Side Top")
                            //left(Cord_Side_Cutouts == "Right Side" ? channelWidth/2 : 0)
                            right(channelWidth/2-Cord_Cutout_Width/2)
                            
                            //up(snapWallThickness) 
                            color_this(Global_Color) 
                            up(topHeight/2-baseHeight+snapWallThickness) yrot(-90)
                                cylinder(h = Channel_Total_Height, r = Cord_Cutout_Width/2, orient=LEFT, anchor=LEFT, $fn=50)
                            attach(RIGHT,BOT, overlap=Cord_Cutout_Width/2) color_this(Global_Color) 
                                cube([Channel_Total_Height, Cord_Cutout_Width, channelWidth-Cord_Cutout_Width/2+Nudge], spin=90);
                        if (Cord_Side_Cutouts == "Right Side Top")
                            //left(Cord_Side_Cutouts == "Right Side" ? channelWidth/2 : 0)
                            // left(channelWidth/2)
                            left(channelWidth/2-Cord_Cutout_Width/2)
                            
                            //up(snapWallThickness) 
                            color_this(Global_Color) 
                            up(topHeight/2-baseHeight+snapWallThickness) yrot(90)
                                cylinder(h = Channel_Total_Height, r = Cord_Cutout_Width/2, orient=LEFT, anchor=LEFT, $fn=50)
                            attach(RIGHT,BOT, overlap=Cord_Cutout_Width/2) color_this(Global_Color) 
                                cube([Channel_Total_Height, Cord_Cutout_Width, channelWidth-Cord_Cutout_Width/2+Nudge], spin=90);
                        if (Cord_Side_Cutouts == "Right Side" || Cord_Side_Cutouts == "Both Sides")
                            //left(Cord_Side_Cutouts == "Right Side" ? channelWidth/2 : 0)
                            left(channelWidth/2)
                            
                            up(snapWallThickness) color_this(Global_Color) 
                            //up(topHeight/2-baseHeight) yrot(-90)
                            cylinder(h = topChamfer*2, r = Cord_Cutout_Width/2, orient=LEFT, anchor=LEFT, $fn=50)
                            attach(RIGHT,BOT, overlap=Cord_Cutout_Width/2) color_this(Global_Color) 
                                cube([topChamfer*2, Cord_Cutout_Width, Channel_Total_Height-snapWallThickness-Cord_Cutout_Width/2], spin=90);
                        if (Cord_Side_Cutouts == "Left Side" || Cord_Side_Cutouts == "Both Sides")
                            left(-channelWidth/2)
                            // cuboid([Cord_Side_Cutouts == "Both Sides" ? channelWidth + 5 : channelWidth/2, Cord_Cutout_Width, Channel_Total_Height-snapWallThickness], chamfer = 2, edges=[BOT+FWD, BOT+BACK], orient=TOP, anchor=BOTTOM);
                            up(snapWallThickness) color_this(Global_Color) 
                            cylinder(h = topChamfer*2, r = Cord_Cutout_Width/2, orient=LEFT, anchor=LEFT, $fn=50)
                            attach(RIGHT,BOT, overlap=Cord_Cutout_Width/2) color_this(Global_Color) 
                                cube([topChamfer*2, Cord_Cutout_Width, Channel_Total_Height-snapWallThickness-Cord_Cutout_Width/2], spin=90);
//                            up(snapWallThickness) color_this(Global_Color) cylinder(h = Cord_Side_Cutouts == "Both Sides" ? channelWidth + 5 : channelWidth/2, r = Cord_Cutout_Width/2, orient=LEFT, anchor=LEFT, $fn=50)
//                            attach(RIGHT,BOT, overlap=Cord_Cutout_Width/2) color_this(Global_Color) cube([Cord_Side_Cutouts == "Both Sides" ? channelWidth + 5 : channelWidth/2, Cord_Cutout_Width, Channel_Total_Height-snapWallThickness-Cord_Cutout_Width/2], spin=90);
                    }
                }

            }
            
if(Add_Label) tag("remove") 
    up(Text_Depth > 0.05 ? Text_Depth/2 : 0.03) right(Text_x_coordinate) text3d(Text, size = Text_size, h=Text_Depth > 0.05 ? Nudge+Text_Depth : 0.06, font = surname_font, atype="ycenter", anchor=CENTER, spin=-90, orient=BOT);
//}

if(Add_Label && !Text_Emboss) tag("keep") recolor(Text_Color)
    up(Text_Depth/2-Nudge) right(Text_x_coordinate) text3d(Text, size = Text_size, h=Text_Depth > 0.05 ? Text_Depth : 0.05, font = surname_font, atype="ycenter", anchor=CENTER, spin=-90, orient=BOT);
}
//    move(v=[widthMM/2,-heightMM/2-addedRadius*Grid_Size,addedRadius*Grid_Size])
left(Added_Radius*Grid_Size)
down(Added_Radius*Grid_Size)
down(lengthMM2/2+Channel_Total_Height/2)
fwd(curveWidth/2-Channel_Total_Height/2-channelWidthSeparation-Added_Radius*Grid_Size) 
color_this(Global_Color) 
zrot(-90) yrot(-90)
  monokiniChannel(lengthMM = lengthMM2, widthMM = channelWidth, heightMM = Channel_Total_Height, anchor = CENTER, orient = TOP, spin = 270, suppress = Suppress_List[1], separator = Separator_List[0]);

color_this(Global_Color) 
monokiniChannelCurve(lengthMM = lengthMM, widthMM = channelWidth, heightMM = Channel_Total_Height, addedRadius = Added_Radius, separator = Separator_List[0]);

}
}

/*

***BEGIN MODULES***

*/
module monokiniGrip(widthMM = 26.4, heightMM = 22, flareAngle = 0) {

        
        snapCaptureStrength = 0.7;
        baseChamfer = 0.5;
        topLittleChamfer = 0.4;
        spacingFromChannel = 6.5;
        
        chamferPosY = spacingFromChannel - (baseHeight - snapWallThickness);
        chamferPosY2 = gripSize + spacingFromChannel + (baseHeight - snapWallThickness);

        

 monokiniProfileGrip =
        [
            [snapCaptureStrength,0],
            [snapCaptureStrength, baseHeight-snapWallThickness],
            [0, baseHeight-snapWallThickness/2],
            [0, baseHeight-topLittleChamfer],
            [snapCaptureStrength, baseHeight],
            [snapWallThickness, baseHeight],
            [snapWallThickness, 0]
        ];


    gripPath = [[-1*widthMM/2,spacingFromChannel],[-1*widthMM/2,gripSize+spacingFromChannel]];
    up(heightMM-baseHeight) 
    //yrot(-Grip_Flare,cp=gripPath[0]) //rotate the grip path to match the flare
    skew(axz=-flareAngle)
    difference() {
        path_extrude2d( gripPath ) 
            polygon(monokiniProfileGrip); 
            //path_extrude(path, shape, anchor=BOTTOM, orient=TOP, spin=0, size=[2,2], $fn=50)
            up(baseHeight)
            left((widthMM/2)-snapWallThickness)
            back(chamferPosY) 
            zrot(-90)
            yrot(180) 
            xrot(90)
            linear_extrude(snapWallThickness) 
            right_triangle([baseHeight,baseHeight]);
            up(baseHeight)
            left(widthMM/2)
            back(chamferPosY2) 
            zrot(90)
            yrot(180) 
            xrot(90)
            linear_extrude(snapWallThickness) 
            right_triangle([baseHeight,baseHeight]);
    }
}

module monokiniChannel(lengthMM = 28, widthMM = 26.4, heightMM = 22, anchor, spin, orient, suppress = "", separator = "") {
    
    // echo("Suppress: ", suppress);
    // zrot(180) xrot(90) path_extrude( monokiniProfile()) square([2,channelWidth]); //path_extrude(path, shape, anchor=BOTTOM, orient=TOP, spin=0, size=[2,2], $fn=50)

off_Profile2 = monokini_offset_profile(widthMM, heightMM);

    pathChannel = [[0,0],[0,lengthMM-Nudge]];
 

    supportProfile = [
            [-Decorative_Height, -Decorative_Height],
            [-0.1,-0.2],
            [0,0],
            [0.1,-0.2],
            [Decorative_Height, -Decorative_Height]
        ];

 
    attachable(anchor, spin, orient, size=[widthMM, lengthMM, topHeight + (heightMM-12)]){
    fwd(lengthMM/2) 
    difference() {
    union() {
    back(lengthMM-Nudge/2) xrot(90) linear_extrude(height=lengthMM+Nudge) 
    //polygon(off_Profile2);
    union() {
                polygon(monokini_offset_profile(widthMM, heightMM));
                rect = monokini_midline_rects(widthMM, heightMM);
                for ($i = [0 : len(rect)-1]) 
                    if (separator[$i] == "S")
                        polygon(rect[$i]);
            }
    if (Decorative) {
        back(lengthMM-Nudge) xrot(90) linear_extrude(height=lengthMM+Nudge) 
        xcopies(l=widthMM-topChamfer*2-Decorative_Height*2-minWall*2, spacing=Support_Bridging_Separation)
        polygon(supportProfile);
    }
    
     if (lengthMM > gripSize)
        path_copies(pathChannel, spacing=Grid_Size) {
            right(Grid_Size/2)
            zrot(90) 
            {
            // echo("Index: ", $idx, " Suppress: ", suppress[$idx]);
                if (suppress[$idx] != "B" && suppress[$idx] != "L")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        monokiniGripFlared(widthMM = widthMM, heightMM = heightMM, flareAngle = Grip_Flare);
                    else
                        down(22-heightMM) monokiniGrip(widthMM = widthMM);
                if (suppress[$idx] != "B" && suppress[$idx] != "R")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        xflip() monokiniGripFlared(widthMM = widthMM, heightMM = heightMM, flareAngle = Grip_Flare);
                    else
                        down(22-heightMM) xflip() monokiniGrip(widthMM = widthMM);
            }
        }
    }
    if (lengthMM > gripSize)
        path_copies(pathChannel, spacing=Grid_Size) {
            left(Grid_Size/2)
            zrot(90) 
            // down(22-heightMM) 
            {
            // echo("Index: ", $idx, " Suppress: ", suppress[$idx]);
                if (suppress[$idx] != "B" && suppress[$idx] != "L")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        monokiniGripFlaredCut(widthMM = widthMM, heightMM = heightMM, flareAngle = Grip_Flare);
                    // else
                        // monokiniGrip(widthMM = widthMM);
                if (suppress[$idx] != "B" && suppress[$idx] != "R")
                    if (Grip_Flare > 0 && suppress[$idx] != "S")
                        xflip() monokiniGripFlaredCut(widthMM = widthMM, heightMM = heightMM, flareAngle = Grip_Flare);
                    // else
                        // xflip() monokiniGrip(widthMM = widthMM);
            }
        }
    } children();
    } 

    
 }


module monokiniGripFlared(widthMM = 26.4, heightMM = 22, flareAngle = 0) {

        
        snapCaptureStrength = 0.7;
        baseChamfer = 0.5;
        topLittleChamfer = 0.4;
        spacingFromChannel = 6.5;
        
        chamferPosY = spacingFromChannel - (baseHeight - snapWallThickness);
        chamferPosY2 = gripSize + spacingFromChannel + (baseHeight - snapWallThickness);

        

 monokiniProfileGrip =
        [
            [snapCaptureStrength,topChamfer],
            [snapCaptureStrength, heightMM-snapWallThickness],
            [0, heightMM-snapWallThickness/2],
            [0, heightMM-topLittleChamfer],
            [snapCaptureStrength, heightMM],
            [snapWallThickness, heightMM],
            [snapWallThickness, topChamfer]
        ];


    gripPath = [[-1*widthMM/2,spacingFromChannel],[-1*widthMM/2,gripSize+spacingFromChannel]];
    //up(heightMM-baseHeight) 
    //yrot(-Grip_Flare,cp=gripPath[0]) //rotate the grip path to match the flare
    skew(axz=-flareAngle)
    difference() {
        path_extrude2d( gripPath ) 
            polygon(monokiniProfileGrip); 
            //path_extrude(path, shape, anchor=BOTTOM, orient=TOP, spin=0, size=[2,2], $fn=50)
            up(heightMM-baseHeight) {
            up(baseHeight)
            left((widthMM/2)-snapWallThickness)
            back(chamferPosY) 
            zrot(-90)
            yrot(180) 
            xrot(90)
            linear_extrude(snapWallThickness) 
            right_triangle([baseHeight,baseHeight]);
            up(baseHeight)
            left(widthMM/2)
            back(chamferPosY2) 
            zrot(90)
            yrot(180) 
            xrot(90)
            linear_extrude(snapWallThickness) 
            right_triangle([baseHeight,baseHeight]);
            }
    }
}

module monokiniGripFlaredCut(widthMM = 26.4, heightMM = 22, flareAngle = 0) {

        
        snapCaptureStrength = 0.7;
        baseChamfer = 0.5;
        topLittleChamfer = 0.4;
        spacingFromChannel = 6.5;
        
        chamferPosY = spacingFromChannel - (baseHeight - snapWallThickness);
        chamferPosY2 = gripSize + spacingFromChannel + (baseHeight - snapWallThickness);

        up(topChamfer)
        left(widthMM/2-Nudge) {
            fwd(spacingFromChannel-(topLittleChamfer/2)) cube([Nudge+snapWallThickness*2, topLittleChamfer, heightMM-baseHeight], anchor=BOTTOM);
            fwd(gripSize+spacingFromChannel+(topLittleChamfer/2)) cube([Nudge+snapWallThickness*2, topLittleChamfer, heightMM-baseHeight], anchor=BOTTOM);
        }
        
/*
 monokiniProfileGrip =
        [
            [snapCaptureStrength,topChamfer],
            [snapCaptureStrength, heightMM-snapWallThickness],
            [0, heightMM-snapWallThickness/2],
            [0, heightMM-topLittleChamfer],
            [snapCaptureStrength, heightMM],
            [snapWallThickness, heightMM],
            [snapWallThickness, topChamfer]
        ];


    gripPath = [[-1*widthMM/2,spacingFromChannel],[-1*widthMM/2,gripSize+spacingFromChannel]];
    //up(heightMM-baseHeight) 
    //yrot(-Grip_Flare,cp=gripPath[0]) //rotate the grip path to match the flare
    skew(axz=-flareAngle)
    difference() {
        path_extrude2d( gripPath ) 
            polygon(monokiniProfileGrip); 
            //path_extrude(path, shape, anchor=BOTTOM, orient=TOP, spin=0, size=[2,2], $fn=50)
            up(heightMM-baseHeight) {
            up(baseHeight)
            left((widthMM/2)-snapWallThickness)
            back(chamferPosY) 
            zrot(-90)
            yrot(180) 
            xrot(90)
            linear_extrude(snapWallThickness) 
            right_triangle([baseHeight,baseHeight]);
            up(baseHeight)
            left(widthMM/2)
            back(chamferPosY2) 
            zrot(90)
            yrot(180) 
            xrot(90)
            linear_extrude(snapWallThickness) 
            right_triangle([baseHeight,baseHeight]);
            }
    }
    */
}

 module monokiniChannelCurve(lengthMM = 28, widthMM = 26.4, heightMM = 22, addedRadius=1, anchor, spin, orient, separator = "") {
    
    
    off_Profile2 = monokini_offset_profile(widthMM, heightMM);

    // zrot(180) xrot(90) path_extrude( monokiniProfile()) square([2,channelWidth]); //path_extrude(path, shape, anchor=BOTTOM, orient=TOP, spin=0, size=[2,2], $fn=50)
/*
    monokiniProfile = [
            [-1*widthMM/2, heightMM-baseHeight],
            [-1*widthMM/2, topChamfer],
            [-1*widthMM/2+topChamfer, Decorative?-Decorative_Height:0],
            [-1*widthMM/2+topChamfer+minWall, Decorative?-Decorative_Height:0],
            [-1*widthMM/2+topChamfer+minWall+Nudge, 0],
            [widthMM/2-topChamfer-minWall-Nudge, 0],
            [widthMM/2-topChamfer-minWall, Decorative?-Decorative_Height:0],
            [widthMM/2-topChamfer, Decorative?-Decorative_Height:0],
            [widthMM/2, topChamfer],
            [widthMM/2, heightMM-baseHeight]
        ];
    pathChannel = list_rotate(list_insert(list_insert(arc(90, r = (addedRadius*Grid_Size) + widthMM/2, angle = [180,90], endpoint= true), 0, [-widthMM/2-(addedRadius*Grid_Size),-channelWidthSeparation-Nudge]), 0, [channelWidthSeparation+Nudge,widthMM/2+(addedRadius*Grid_Size)]));

*/
    pathChannel = list_rotate(list_insert(list_insert(arc(90, r = (addedRadius*Grid_Size) + heightMM/2, angle = [180,90], endpoint= true), 0, [-heightMM/2-(addedRadius*Grid_Size),-channelWidthSeparation-Nudge]), 0, [channelWidthSeparation+Nudge,heightMM/2+(addedRadius*Grid_Size)]));
/*    
    roff_Profile = offset(monokiniProfile, delta=-snapWallThickness, check_valid=true); //create the monokini profile
    point1 = select(monokiniProfile, 0); //get the first point of the profile
    point2 = select(roff_Profile, 0); //get the first point of the offset profile
    point3 = select(roff_Profile, -1); //get the last point of the offset profile
    roff_Profile2 = list_set(roff_Profile, 0, [point2[0], point1[1]]); //fix the first point to be the same as the original profile
    roff_Profile3 = list_set(roff_Profile2, -1, [point3[0], point1[1]]); //fix the first point to be the same as the original profile

    off_Profile2 = concat(monokiniProfile,reverse(roff_Profile3)); //reverse the order of the points to match the original profile
*/

supportProfile = [
            [-Decorative_Height, -Decorative_Height],
            [-0.1,-0.2],
            [0,0],
            [0.1,-0.2],
            [Decorative_Height, -Decorative_Height]
        ];
    // stroke(off_Profile2);
    //attachable(anchor, spin, orient, size=[widthMM, lengthMM, topHeight + (heightMM-12)]){
    // fwd(lengthMM/2) 
   
    union() {
    // back(lengthMM-Nudge/2) xrot(90) linear_extrude(height=lengthMM+Nudge) 
 
    
 
 //   back_half(s = max(widthMM*2, heightMM*2), y=-Nudge) 
 //   left_half(s = max(widthMM*2, heightMM*2), x=Nudge) 
    zrot(-90)xrot(90)
    move(v=[widthMM/2,-heightMM/2-addedRadius*Grid_Size,addedRadius*Grid_Size]) 
     path_extrude2d(pathChannel)
    zrot(90)
    union() {
                polygon(monokini_offset_profile(widthMM, heightMM));
                rect = monokini_midline_rects(widthMM, heightMM);
                for ($i = [0 : len(rect)-1]) 
                    if (separator[$i] == "S")
                        polygon(rect[$i]);
            }
 
if (Decorative) {
        //back(lengthMM-Nudge) xrot(90) linear_extrude(height=lengthMM+Nudge)
        move(v=[widthMM/2,-widthMM/2,0]) 
 //   back_half(s = max(widthMM*2, heightMM*2), y=-Nudge) 
 //   left_half(s = max(widthMM*2, heightMM*2), x=Nudge) 
    path_extrude2d(pathChannel)
        xcopies(l=widthMM-topChamfer*2-Decorative_Height*2-minWall*2, spacing=Support_Bridging_Separation)
        polygon(supportProfile);
    }

    } children();
    //} 
 }

function monokini_offset_profile(widthMM, heightMM) =
    let(
        left_half = [
            [-widthMM/2, heightMM-3.40],
            [-widthMM/2, topChamfer],
            [-widthMM/2+topChamfer, Decorative?-Decorative_Height:0],
            [-widthMM/2+topChamfer+minWall, Decorative?-Decorative_Height:0],
            [-widthMM/2+topChamfer+minWall+Nudge, 0]
        ],
        right_half = [for (p = reverse(left_half)) [ -p[0], p[1] ]],
        profile = concat(left_half, right_half),
        roff_Profile_init = offset(deduplicate(profile), delta=-snapWallThickness, check_valid=true),
        roff_Profile_fixed =
            (select(roff_Profile_init,0)[1] != select(profile,0)[1] || select(roff_Profile_init,-1)[1] != select(profile,0)[1])
            ? list_set(list_set(roff_Profile_init, 0, [select(roff_Profile_init,0)[0], select(profile,0)[1]]), -1, [select(roff_Profile_init,-1)[0], select(profile,0)[1]])
            : roff_Profile_init
        // roff_Profile3 = concat(profile, reverse(roff_Profile_fixed)) //removes any duplicate points
    )
    concat(profile, reverse(roff_Profile_fixed));

// Returns points along the midline (Y=0) at intervals of Grid_Size/2 that fit inside the profile width
function monokini_midline_points(widthMM, heightMM) =
    [for (x = [ -widthMM/2 : Grid_Size/2 : widthMM/2 ]) [x, heightMM-3.40]];

// Returns rectangles (as list of 4 points) along the midline at intervals of Grid_Size/2, for union/intersection with the profile
function monokini_midline_rects(widthMM, heightMM) =
    let(
    left_half = [for (x = [ 0 : Grid_Size/2 : widthMM/2 ])
        [
            [x-snapWallThickness/2, Decorative?-Decorative_Height:0],
            [x-snapWallThickness/2, heightMM-3.40],
            [x+snapWallThickness/2, heightMM-3.40],
            [x+snapWallThickness/2, Decorative?-Decorative_Height:0]
        ]
    ],
    right_half = [for (rect = reverse(left_half)) [for (p = rect) [ -p[0], p[1] ]]],
    rects = concat(left_half, right_half)
    )
    deduplicate(rects);


//calculate the max x and y points. Useful in calculating size of an object when the path are all positive variables originating from [0,0]
function maxX(path) = max([for (p = path) p[0]]) + abs(min([for (p = path) p[0]]));
function maxY(path) = max([for (p = path) p[1]]) + abs(min([for (p = path) p[1]]));
